apply plugin: 'com.android.application'

static def gitLatestTag() {
    try {
        def cmd = 'git describe --abbrev=0 --tags'
        def result = cmd.execute().text.trim()
        return result
    } catch (Exception e) {
        return '1.0.0'
    }
}

android {
    namespace "me.yokeyword.sample"
    compileSdk rootProject.compileSdkVersion

    defaultConfig {
        applicationId "me.xuexuan.fragmentation"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 1
        versionName gitLatestTag()
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }


    android.applicationVariants.all { variant ->
        variant.outputs.all {
            if (variant.buildType.name == 'release') {
                outputFileName = "fragmentation.apk"
            }
        }
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.13.2'
    implementation "androidx.appcompat:appcompat:1.4.1"
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    api project(':fragmentation')
    api project(':fragmentation_swipeback')

//    api 'me.xuexuan:fragmentationx:1.0.6'
//    api 'me.xuexuan:fragmentationx-swipeback:1.0.6'
    // EventBus
    api project(':eventbus_activity_scope')
    implementation 'org.greenrobot:eventbus:3.0.0'
    //    apt 'org.greenrobot:eventbus-annotation-processor:3.0.1'
}

